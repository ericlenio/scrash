set nocompatible

if has("autocmd")
  " vnoremap: 'v' visual mode, 'nore' no recurse, 'map' map
  " here we remap control-c to copy the current visual selection to the OS clipboard
  autocmd BufReadPre,FileReadPre * vnoremap <c-c> y:call Cp2cb()<cr>
  autocmd BufReadPre,FileReadPre * map <c-v> :call PasteCb()<cr>

  " when editing the passwords file, remap * to copy the current word to OS clipboard
  " use the expand() function to highlight the current word (cword)
  " nmap: "n" means we are mapping a key in vim's normal mode
  autocmd BufReadPre,FileReadPre *.gpg nmap <buffer> * :let @/=expand("<cword>")<cr>:let @"=@/<cr>:call Cp2cb()<cr>

  " Transparent editing of gpg encrypted files.
  " By Wouter Hanegraaff
  augroup encrypted
    au!

    " First make sure nothing is written to ~/.viminfo while editing
    " an encrypted file.
    autocmd BufReadPre,FileReadPre *.gpg set viminfo=
    " We don't want a swap file, as it writes unencrypted data to disk
    autocmd BufReadPre,FileReadPre *.gpg set noswapfile

    " Switch to binary mode to read the encrypted file
    autocmd BufReadPre,FileReadPre *.gpg set bin
    autocmd BufReadPre,FileReadPre *.gpg let ch_save = &ch|set ch=2
    " (If you use tcsh, you may need to alter this line.)
    autocmd BufReadPost,FileReadPost *.gpg '[,']!gpg -d 2>/dev/null

    " Switch to normal mode for editing
    autocmd BufReadPost,FileReadPost *.gpg set nobin
    autocmd BufReadPost,FileReadPost *.gpg let &ch = ch_save|unlet ch_save
    autocmd BufReadPost,FileReadPost *.gpg execute ":doautocmd BufReadPost " . expand("%:r")

    " Convert all text to encrypted text before writing
    " (If you use tcsh, you may need to alter this line.)
    autocmd BufWritePre,FileWritePre *.gpg '[,']!gpg --default-recipient-self -ae 2>/dev/null
    " Undo the encryption so we are back in the normal text, directly
    " after the file has been written.
    autocmd BufWritePost,FileWritePost *.gpg u
  augroup END
endif

fu Mktmpdir()
  " make sure the temp directory exists (for long running vim sessions,
  " because a cron job might delete it) - any time we do a vim "system"
  " command this function should be called first
  let @a=fnamemodify(tempname(),":p:h")
  if ! isdirectory(@a)
    call mkdir(@a,"p",0700)
  endif
endf

fu PasteCb()
  " paste contents of OS clipboard
  try
    let [@",l:stderr,l:rc]=Systemcaller("-clipboard")
    if len(l:stderr) > 0
      echohl WarningMsg
      echon join(l:stderr," ")
      echohl None
      echon " "
    endif
    if l:rc == 0
      call feedkeys(col("$")-col(".")==1 ? "p" : "P")
      "redraw
      echon "pasted " len(@") " characters"
    endif
  catch /.*/
    echo v:exception
  endtry
endf

" returns a list of: stdout, stderr, and shell return code
fu Systemcaller(cmd,...)
  call Mktmpdir()
  " have stderr lines get prefixed with "stderr:" - also see shellredir
  let cmd=a:cmd." 2> >(while true; do read -r l; rc=$?; [ ${#l} -gt 0 ] && echo \"stderr:$l\"; [ $rc -gt 0 ] && break; done)"
  if a:0 == 1
    let l:result=system(cmd,a:1)
  else
    let l:result=system(cmd)
  endif
  let l:stdout=""
  let l:stderr=[]
  for l:line in split(l:result,'\n')
    if l:line =~ '^stderr:'
      let l:stderr+=[l:line]
    else
      if len(l:stdout) > 0
        let l:stdout.="\n"
      endif
      let l:stdout.=l:line
    endif
  endfor
  return [l:stdout,l:stderr,v:shell_error]
endf

fu Cp2cb()
  " copies the contents of the unnamed register to OS clipboard;
  " NOTE: the system command will write out to a temp file
  " which is vim's tempname() function
  try
    let [l:stdout,l:stderr,l:rc]=Systemcaller("-copy-to-clipboard",@")
    if len(l:stderr) > 0
      echohl WarningMsg
      echon join(l:stderr," ")
      echohl None
      echon " "
    endif
    if l:rc == 0
      echon "Copied " len(@") " characters"
    endif
  catch /.*/
    echo v:exception
  endtry
endf

colorscheme elflord
syntax enable
set cursorline noswapfile number hlsearch incsearch sw=2 ignorecase wrap
set ruler ai nocindent indentexpr= showmatch modeline modelines=5 nobackup nowritebackup textwidth=0
set expandtab formatoptions=croql nojoinspaces scrolloff=0 laststatus=2
set wildmenu wildignore=*.o,*.class,*.jar,*.png,*.pdf,$PWD/**node_modules/**,build/**,.git/**,.svn/**,dist/**,tmp/** path=.,,**
filetype indent off
